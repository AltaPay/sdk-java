// Publish module to Maven

plugins {
    id "maven-publish"
    id "java"
    id "signing"
}

task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = "javadoc"
    from javadoc
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "com.altapay"
            artifactId = "sdk-java"
            version = "1.0.20"
            artifact "output/javaSDK_${version}.jar"
            artifact sourceJar
            artifact javadocJar

            pom {
                name = "sdk-java"
                description = "JavaSDK - for integrating Java projects with the AltaPay gateway."
                url  = "https://github.com/AltaPay/sdk-java"
                scm {
                  connection =  "scm:https://github.com/AltaPay/sdk-java.git"
                  developerConnection = "scm:git://github.com:AltaPay/sdk-java.git"
                  url = "https://github.com/AltaPay/sdk-java"
                }
                licenses {
                  license {
                    name = "MIT License"
                    url = "https://github.com/AltaPay/sdk-java/blob/master/LICENSE"
                  }
                }
                developers {
                  developer {
                    id = "altapay"
                    name = "AltaPay"
                    email = "3rdpartydeps@altapay.com"
                  }
              }
            }
        }
    }
    repositories {
      maven {
          credentials {
              username = "$usr"
              password = "$pwd"
          }

          url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      }
  }
}

signing {
    sign publishing.publications.maven
}
